#!/bin/zsh
emulate -L zsh
zmodload zsh/complist
zmodload -Fa zsh/zleparameter p:widgets
zmodload -Fa zsh/zutil b:zformat
autoload -Uz add-zsh-hook
setopt $_edit_opts

local -a opts mopt
zparseopts -A opts -D -F -- d n u M:=mopt ||
    return

if [[ -v opts[-d] ]]; then
  local -a bindings dup_bindings=() all_bindings=( "${(f)$( bindkey $mopt[@] $@ )}" )
  local w
  for w in "${(@ko)widgets}"; do
    bindings=( ${(M)all_bindings:#* $w}  )
    (( $#bindings > 1 )) &&
        dup_bindings+=( ${w}:${(j.:.)${${(n)bindings[@]:/(#m)*/$#MATCH$'\0'$MATCH}[@]#*$'\0'}[@]% $w} )
  done
  while [[ $dup_bindings[(I)*:*] -gt 0 ]]; do
    zformat -a dup_bindings ' ' "$dup_bindings[@]"
  done
  print -lr "$dup_bindings[@]"
elif [[ -v opts[-n] ]]; then
  local -A names=()
  local -a match=() mbegin=() mend=() out=()
  local key= code= name= expl=
  for key; do
    match=() name= code="${(g:ceo:)key}"
    while [[ -n $code ]]; do
      [[ -n $name && $name != *- ]] &&
          name+=' + '
      case $code in
        ( (#b)($'\e'[\[O]([ABCDFHZ]))* )
          case $match[2] in
            ( A ) name+='Up' ;;
            ( B ) name+='Down' ;;
            ( C ) name+='Right' ;;
            ( D ) name+='Left' ;;
            ( F ) name+='End' ;;
            ( H ) name+='Home' ;;
            ( Z ) name+='Shift-Tab' ;;
          esac
        ;;
        ( (#b)($'\e['([356])\~)* )
          case $match[2] in
            ( 3 ) name+='Delete' ;;
            ( 5 ) name+='PageUp' ;;
            ( 6 ) name+='PageDown' ;;
          esac
        ;;
        ( (#b)($'\e'(O|\[|\[[1356]\;|\[27\;)(|<2-16>)([ABCDFH~]|\;8~))* )
          case $match[3] in
            (  2 ) name+='Shift' ;;
            (  3 ) name+='Alt' ;;
            (  4 ) name+='Alt-Shift' ;;
            (  5 ) name+='Ctrl' ;;
            (  6 ) name+='Ctrl-Shift' ;;
            (  7 ) name+='Ctrl-Alt' ;;
            (  8 ) name+='Ctrl-Alt-Shift' ;;
            (  9 ) name+='Meta' ;;
            ( 10 ) name+='Meta-Shift' ;;
            ( 11 ) name+='Meta-Alt' ;;
            ( 12 ) name+='Meta-Alt-Shift' ;;
            ( 13 ) name+='Ctrl-Meta' ;;
            ( 14 ) name+='Ctrl-Meta-Shift' ;;
            ( 15 ) name+='Ctrl-Meta-Alt' ;;
            ( 16 ) name+='Ctrl-Meta-Alt-Shift' ;;
          esac
          name+='-'
          case $match[4] in
            ( A     ) name+='Up'        ;;
            ( B     ) name+='Down'      ;;
            ( C     ) name+='Right'     ;;
            ( D     ) name+='Left'      ;;
            ( F     ) name+='End'       ;;
            ( H     ) name+='Home'      ;;
            ( \;8~  ) name+='Backspace' ;;
            ( \~    )
              case $match[2] in
                ( '[3;' ) name+='Delete'    ;;
                ( '[5;' ) name+='PageUp'    ;;
                ( '[6;' ) name+='PageDown'  ;;
              esac
            ;;
          esac
        ;;
        ( (#b)(\+)*       ) name+='Plus' ;;
        ( (#b)(\-)*       ) name+='Minus' ;;
        ( (#b)($'\C-_')*  ) name+="Control-Minus" ;;
        ( (#b)(\ )*       ) name+='Space' ;;
        ( (#b)($'\C-@')*  ) name+='Control-Space' ;;
        ( (#b)($'\C-?')*  ) name+='Backspace' ;;
        ( (#b)($'\b')*    ) name+='Control-Backspace' ;;
        ( (#b)($'\n')*    ) name+="Enter" ;;
        ( (#b)($'\r')*    ) name+="Return" ;;
        ( (#b)($'\t')*    ) name+="Tab" ;;
        ( (#b)($'\e')     ) name+="Escape" ;;
        ( (#b)($'\e')*    ) name+="Alt-" ;;
        ( (#b)([$'\C-A'-$'\C-^'])* ) name+="Control-${(SM)${(q+)match[1]}%[A-_]}" ;;
        ( (#b)([[:upper:]])* ) name+="Shift-$match[1]" ;;
        ( (#b)([[:graph:]])* ) name+="${(U)match[1]}" ;;
        ( * )
          name+='??'
        ;;
      esac
      code="${code#$match[1]}"
    done
    names["$key"]=$name
  done
  for key; do
    out+=( ${(M)"$( bindkey $key )"##\"*\" } $names["$key"] )
  done
  print -arC2 -- "$out[@]"

elif [[ -v opts[-u] ]]; then
  local -a val=() keys=(
    \  {a..z} \; \' \, \. \/ \[ \] \\ \` {1..9}                      0 \- \= $'\C-?'
    $'\e'{O,\[}{A,B,C,D,F,H} $'\e['{2,3,5,6}\~
       {A..Z} \: \" \< \> \? \{ \} \| \~ \! \@ \# \$ \% \^ \& \* \( \) \_ \+ $'\e[Z'
    {$'\C-@'..$'\C-_'}
  )
  local k
  for k in $keys $'\e'$^keys; do
    [[ $( bindkey $mopt[@] $@ - $k )$( bindkey $mopt[@] $@ -p - $k ) == *'undefined-key' ]] &&
        val+=( ${${${${${(q+)k}//'\C-'/^}//'\\'/\\}#(|\$)[\']}%[\']} )
  done
  (( $#val )) &&
      print -cr - ${(q-)val}

else
  # Start each widget name with `.` to make autosuggestions & syntax highlighting ignore them.
  local -i i=
  for (( i = 2; i <= $#; i += 2 )); do
    argv[i]=".${(qq)argv[i]}"
    zle -N "$argv[i]" .edit.execute-cmd
  done
  bindkey $mopt[@] "$@"
fi
