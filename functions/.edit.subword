#!/bin/zsh
emulate -L zsh; setopt $_edit_opts
local -i move=0

local tradback
zstyle -b ":edit:$WIDGET:" traditional-backward-movement tradback

if [[ $WIDGET == *kill-* ]]; then
  zle -f kill
  if (( REGION_ACTIVE )); then
    zle .kill-region
    return
  fi
fi

if [[ $WIDGET == *-shell-word ]]; then
  local w=
  if [[ $WIDGET == *backward* ]]; then
    local -a words=( ${(z)LBUFFER} )
    if [[ $tradback != yes ]]; then
      while [[ -z $w && $#words[@] -gt 0 ]]; do
        if [[ $words[-1] == \; ]]; then
          w=${(M)LBUFFER%[$';\n']*}
        else
          w=${(M)LBUFFER%$words[-1]*}
        fi
        w=${(M)LBUFFER%%[[:blank:]]#$w}
        shift -p words
      done
    else
      if [[ $#words -gt 0 ]]; then
        w=${(M)LBUFFER%${words[-1]}[[:blank:]]#}
      else
        w=$LBUFFER
      fi
    fi
    move=-$#w
  else
    # We can't split $RBUFFER on words, because that usually doesn't parse correctly.
    local -a words=( ${(z)BUFFER} ) lwords=( ${(z)LBUFFER} )
    words=( $words[$#lwords[@],-1] )
    words[1]=${words[1]#$lwords[-1]}
    while [[ -z $w && $#words[@] -gt 0 ]]; do
      if [[ $words[1] == \; ]]; then
        w=${(M)RBUFFER#*[$';\n']}
      else
        w=${(M)RBUFFER#*$words[1]}
      fi
      shift words
    done
    move=+$#w
  fi
else
  local wordchars word
  zstyle -s ":edit:$WIDGET:" word-chars wordchars &&
      local +h WORDCHARS="$wordchars"

  if [[ $WIDGET == *backward-* ]]; then
    if [[ $tradback != yes ]]; then
      word='([[:space:]]~[[:WORD:]])#([^[:WORD:][:space:]])#[[:upper:]]#([[:WORD:]]~[[:upper:]])#'
    else
      word='[[:upper:]]#([[:WORD:]]~[[:upper:]])#([[:space:]]~[[:WORD:]])#([^[:WORD:][:space:]])#'
    fi
    move=-${(M)#LBUFFER%%$~word}
  else
    word='([[:space:]]~[[:WORD:]])#([^[:WORD:][:space:]])#[[:upper:]]#([[:WORD:]]~[[:upper:]])#'
    move=+${(M)#RBUFFER##$~word}
  fi
fi

if [[ $WIDGET == *kill-* ]]; then
  # Move the mark instead of the cursor, or else kill-region will add the kill to the wrong end of the cutbuffer.
  (( MARK = CURSOR + move ))
  zle .kill-region
else
  (( CURSOR += move ))
fi

return 0
